plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '2.0.1'
}

allprojects {
	group 'com.pet'
	version '1.0'

	tasks.withType(JavaCompile) {
		// incubating feature btw
		options.incremental = true
	}
}

sourceCompatibility = 9

repositories {
	mavenCentral()
}

jar {
	manifest {
		attributes(
				'Main-Class': 'webshop.app.App'
		)
	}
}

dependencies {
	compile project(':webshop-app')
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.2.1'
	distributionType = Wrapper.DistributionType.ALL
}

// to make gradle produce Java 9 modules for all subprojects
subprojects {

	afterEvaluate {
		repositories {
			jcenter()
		}

		compileJava {
			inputs.property('moduleName', moduleName)
			doFirst {
				options.compilerArgs = [
						'--module-path', classpath.asPath,
				]
				classpath = files()
			}
		}

		compileTestJava {
			inputs.property('moduleName', moduleName)
			doFirst {
				options.compilerArgs = [
						'--module-path', classpath.asPath,
						'--add-modules', 'junit',
						'--add-reads', '$moduleName=junit',
						'--patch-module', '$moduleName=' + files(sourceSets.test.java.srcDirs).asPath,
				]
				classpath = files()
			}
		}

		test {
			inputs.property('moduleName', moduleName)
			doFirst {
				jvmArgs = [
						'--module-path', classpath.asPath,
						'--add-modules', 'ALL-MODULE-PATH',
						'--add-reads', '$moduleName=junit',
						'--patch-module', '$moduleName=' + files(sourceSets.test.java.outputDir).asPath,
				]
				classpath = files()
			}
		}
	}
}